plugins {
    id "com.jfrog.bintray" version "1.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'

group = 'org.hildan.utils'
version = '1.0.0'
sourceCompatibility = '1.6'

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.+'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

bintray {
	user = project.bintrayUsername // taken from gradle.properties
	key = project.bintrayApiKey // taken from gradle.properties
	configurations = ['archives']
	
    publish = true // the version should be auto published after an upload
	
	pkg {
	    repo = project.bintrayRepoLibs // taken from gradle.properties
	    name = project.name
	    desc = 'A library to make your console output look nice, because little things matter.'
	    
	    websiteUrl = 'https://github.com/joffrey-bion/console-drawing'
	    issueTrackerUrl = 'https://github.com/joffrey-bion/console-drawing/issues'
	    vcsUrl = 'https://github.com/joffrey-bion/console-drawing.git'
	    
	    licenses = ['MIT']
	    labels = ['console', 'print', 'grid', 'progress-bar', 'progress', 'bar']
	    
	    publicDownloadNumbers = false
	    
	    version {
	        name = project.version // Bintray logical version name
	    }
	}
}